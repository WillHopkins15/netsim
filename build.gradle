plugins {
    id 'java'
    id 'application'
    id "io.freefair.lombok" version "5.3.0"
}

repositories {
    mavenCentral()
}

def currentOS = org.gradle.nativeplatform.platform.internal.DefaultNativePlatform.currentOperatingSystem
def platform
if (currentOS.isWindows()) {
    platform = 'win'
} else if (currentOS.isLinux()) {
    platform = 'linux'
} else if (currentOS.isMacOsX()) {
    platform = 'mac'
}

java {
    modularity.inferModulePath = true
}

ext.moduleName = 'org.netsim'

compileJava {
    inputs.property('moduleName', moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath
        ]
        classpath = files()
    }
}

dependencies {
    implementation "org.openjfx:javafx-base:15.0.1:${platform}"
    implementation "org.openjfx:javafx-controls:15.0.1:${platform}"
    implementation "org.openjfx:javafx-graphics:15.0.1:${platform}"
    implementation "org.openjfx:javafx-fxml:15.0.1:${platform}"
}

test {
    useJUnitPlatform()
}

application {
    mainModule = "org.netsim"
    mainClass = "org.netsim.NetworkSimulator"
}